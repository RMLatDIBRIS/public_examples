// priority-queue2
// single priority queue with repetitions, enqueue and dequeue 
// works with traces generated from queue-with-rep.js

enq(val) matches {event:'func_pre',name:'enqueue',args:[val]};
deq(val) matches  {event:'func_post',name:'dequeue',res:val};

deq_geq(val) matches deq(val2) with val2 >= val;
enq_deq_geq(val) matches enq(val) | deq_geq(val); // at the moment limited syntax for interaction between '|' and 'with'
deq matches deq(_);
relevant matches deq | enq(_);

Main=relevant>>(Queue/\Sorted)!;

Queue = {let val; enq(val) (deq(val) | Queue)}; 
Sorted = {let val; enq(val) ((enq_deq_geq(val) >> Cons<val> all) /\ Sorted)} \/ (deq Sorted);
Cons<val> = deq(val) \/ (enq(val) (deq(val) | Cons<val>));

